/*-------------------------------------------------------------------*/
/* Envoi d'une bibliothèque par FTP                                  */
/*-------------------------------------------------------------------*/
 PGM        PARM(&SYS &USR &PWD &obj &LIB &type &LIBF &REPERT &PORT +
          &rstobj         &DLTSAVF &CLRLOG &version)
/* PARAMETRES */
             DCL        &SYS *CHAR 80
             DCL        &USR *CHAR 10
             DCL        &PWD *CHAR 50
             DCL        &obj *CHAR 10
             DCL        &LIB *CHAR 10
             DCL        &type *CHAR 08
             DCL        &LIBF *CHAR 10
             DCL        &REPERT *CHAR 50
             DCL        &port   *dec  (5 0)
             DCL        &dltsavf *CHAR 04
             DCL        &clrlog *CHAR 04
             DCL        &rstobj *CHAR 04
             DCL        &version *CHAR 08
/* VARIABLES DE TRAVAIL */
             DCL        &TIM  *CHAR 6
             DCL        &dat  *CHAR 6
             INCLUDE    SRCMBR(ERREUR1)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIM)
             RTVSYSVAL  SYSVAL(Qdate) RTNVAR(&dat)

/*-----------------------------------------------------------------*/
/* CREATION DU FICHIER DE SAUVEGARDE   ET SAUVEGARDE               */
/*-----------------------------------------------------------------*/
             CRTSAVF    &LIBF/&OBJ
             MONMSG     CPF7302 exec(do)
                ClrSAVF    &LIBF/&obj
             enddo

/*-----------------------------------------------------------------*/
/* MODIFICATION DES DROITS PUBLICS                                 */
/*-----------------------------------------------------------------*/
             SAVOBJ     OBJ(&OBJ) LIB(&LIB) DEV(*SAVF) +
                          OBJTYPE(&TYPE) SAVF(&LIBF/&OBJ) +
                          PVTAUT(*YES) DTACPR(*HIGH)  tgtrls(&version)
/* Création Fichier de scripte FTP */
             CRTPF   FILE(QTEMP/QFTPSRC) RCDLEN(192)
             MONMSG     CPF7302  EXEC(DO)
                CLRPFM     FILE(QTEMP/QFTPSRC)
             ENDDO
/* Création Fichier de log FTP */
             CRTSRCPF   FILE(QTEMP/QFTPLOG) RCDLEN(192) MBR(SAVFLG)
             MONMSG     CPF7302  EXEC(DO)
                CLRPFM     FILE(QTEMP/QFTPLOG) MBR(SAVFLG)
                GOTO       SUITE
             ENDDO

 SUITE:
/*-----------------------------------------------------------------*/
/* substitution INPUT par qftpsrc                                  */
/*-----------------------------------------------------------------*/
     OVRDBF     FILE(INPUT) TOFILE(QTEMP/QFTPSRC)
/*-----------------------------------------------------------------*/
/* CREATION DU scripte ftp                                         */
/*-----------------------------------------------------------------*/

             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC VALUES(''' +
                         *CAT &USR *BCAT &PWD *CAT ''')') COMMIT(*NONE)
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC VALUES(''' +
                          *TCAT 'lcd' *BCAT &LIBF *CAT ''')') +
                          COMMIT(*NONE)
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC VALUES(''' +
                          *TCAT 'cd' *BCAT &repert *CAT ''')') +
                          COMMIT(*NONE)
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC +
                          VALUES(''BIN'')') COMMIT(*NONE)
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC VALUES('' +
              quote rcmd CRTSAVF' *bCAT &LIBf *TCAT +
                          '/' *TCAT &obj *TCAT +
                          ''')') COMMIT(*NONE)
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC VALUES(''' +
                          *TCAT 'put' *BCAT &obj *tcat '.savf' +
                    *bcat  &obj *tcat '.savf'')') +
                         COMMIT(*NONE)
 /* syscmd     RSTLIB SAVLIB(TstFTP) DEV(*SAVF) SAVF(TSTFTP) */
if cond(&rstobj  *eq '*YES') then(do)
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC VALUES('' +
               quote rcmd RSTOBJ SAVLIB(' *TCAT &lib *TCAT ') +
                          OBJ(' *TCAT &OBJ *TCAT ')  DEV(*SAVF)  +
  SAVF(' *TCAT &libf *tcat '/' *tcat &OBJ *TCAT ')'')') COMMIT(*NONE)
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC VALUES('' +
              quote rcmd DLTF' *bCAT &LIBf *TCAT +
                          '/' *TCAT &obj *TCAT +
                          ''')') COMMIT(*NONE)
enddo
             RUNSQL     SQL('INSERT INTO QTEMP/QFTPSRC +
                          VALUES(''QUIT'')') COMMIT(*NONE)

/*-----------------------------------------------------------------*/
/* EXECUTION DU SCRIPTE FTP                                        */
/*-----------------------------------------------------------------*/
             OVRDBF     FILE(OUTPUT) TOFILE(QTEMP/QFTPLOG) MBR(SAVFLG)
             FTP        RMTSYS(&SYS) PORT(&PORT)
             DLTOVR     INPUT
             MONMSG     CPF0000
             DLTOVR     OUTPUT
             MONMSG     CPF0000
/* Effacement log    */
if cond(&clrlog   *eq '*YES') then(do)
             CLRPFM     FILE(QTEMP/QFTPLOG) MBR(SAVFLG)
             enddo
             else do
             CHGPFM     FILE(QTEMP/QFTPLOG) MBR(SAVFLG) TEXT('OBJ' +
                          *BCAT &OBJ *BCAT &TIM *BCAT &DAT)
             enddo
/* Suppression  SAVF */
if cond(&dltsavf  *eq '*YES') then(do)
             DLTF       &LIBF/&obj
             enddo

             INCLUDE    SRCMBR(ERREUR2)

             ENDPGM
